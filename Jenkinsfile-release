#!/usr/bin/env groovy
REGISTRIES = [
        [
                url       : 'registry.hub.docker.com',
                credential: 'DockerHub-Infinispan'
        ],
        [
                url       : 'quay.io',
                credential: 'Quay-Infinispan'
        ]
]

IMAGE_CONFIG = [
        server         : [
                descriptor        : 'server-openjdk.yaml',
                'config-generator': [
                        name  : 'config-generator-src',
                        url   : "https://github.com/infinispan/infinispan-image-artifacts/archive/${params.artifactsVersion}.tar.gz",
                        target: 'config-generator-src.tar.gz'
                ],
                server            : [
                        name: 'server',
                        url : "https://downloads.jboss.org/infinispan/%s/infinispan-server-${params.ispnVersion}.zip"
                ]
        ],
        'server-native': [
                descriptor        : 'server-native.yaml',
                'config-generator': [
                        name  : 'config-generator-src',
                        url   : "https://github.com/infinispan/infinispan-image-artifacts/archive/${params.artifactsVersion}.tar.gz",
                        target: 'config-generator-src.tar.gz'
                ],
                server            : [
                        name  : 'server-src',
                        url   : "https://github.com/infinispan/infinispan-quarkus/archive/${params.ispnVersion}.tar.gz",
                        target: 'server-src.tar.gz'
                ]
        ]
]

void updateServerDescriptor(imageName, image) {
    def descriptor = image.descriptor
    def yaml = readYaml file: descriptor
    def builder = yaml[0]
    def runner = yaml[1]

    updateImageVersion image, yaml.version
    builder.version = image.version
    runner.version = image.version

    if (params.artifactsVersion)
        builder.artifacts[0] = image['config-generator']

    if (imageName == 'server-native') {
        builder.artifacts[1] = image.server
    } else {
        runner.artifacts[1] = image.server
    }

    runner.labels.findAll {it.name == 'version' || it.name == 'release'}.each { label ->
        label.value = params.ispnVersion
    }

    sh "rm ${descriptor}"
    writeYaml file: descriptor, data: yaml
}

void updateImageVersion(image, version) {
    def buildNumber = 1
    if (version.contains(params.ispnVersion))
        buildNumber += version.tokenize('-').last() as Integer

    image.version = "${params.ispnVersion}-${buildNumber}"
}

void dockerTagAndPush(String srcTag, String destTag) {
    sh "docker tag ${srcTag} ${destTag}"
    sh "docker push ${destTag}"
}

pipeline {
    agent {
        label 'slave-group-graalvm'
    }

    parameters {
        string(name: 'ispnVersion', description: 'Version of Infinispan artifact to be used')
        string(name: 'artifactsVersion', defaultValue: '', description: 'Image Artifacts Release version (blank to use existing release)')
        string(name: 'images', defaultValue: 'server, server-native', description: 'A comma-separated list of images to be released')
        booleanParam(name: 'pushLatestTag', defaultValue: false, description: 'If true, updates the :latest tag to equal this release for each selected image')
        booleanParam(name: 'dryRun', defaultValue: false, description: 'If true, we don\'t deploy to image repositories')
        gitParameter(name: 'branch', defaultValue: 'origin/master', branchFilter: 'origin/(.*)', type: 'PT_BRANCH', description: 'Branch to release from')
    }

    stages {

        stage('Checkout') {
            steps {
                checkout scm
                sh "git checkout ${params.branch}"
            }
        }

        stage('Update Version and Artifacts') {
            steps {
                script {
                    IMAGES = [:]
                    def images = params.images.contains(',') ? params.images.split(',') : [params.images]
                    images.each {i ->
                        def imageName = i.trim()
                        if (!IMAGE_CONFIG.containsKey(imageName)) {
                            currentBuild.result = 'ABORTED'
                            error("Unknown image '${imageName}'")
                        }
                        IMAGES[imageName] = IMAGE_CONFIG[imageName]
                        updateServerDescriptor imageName, IMAGES[imageName]
                    }
                }
            }
        }

        stage('Build') {
            steps {
                script {
                    IMAGES.each { name, image ->
                        sh "cat ${image.descriptor}"
                        sh "cekit --descriptor ${image.descriptor} build docker"
                    }
                }
            }
        }

        stage('Deploy Release') {
            when {
                expression { !params.dryRun }
            }
            steps {
                script {
                    IMAGES.each { name, image ->
                        REGISTRIES.each { registry ->
                            withDockerRegistry([ credentialsId: registry.credential, url: "https://${registry.url}" ]) {
                                def imageBuild = "infinispan/${name}"
                                def fqImageName = "${registry.url}/${imageBuild}"
                                // Create/update Infinispan version tag e.g. 10.0.1.Final
                                dockerTagAndPush imageBuild, "${fqImageName}:${params.ispnVersion}"

                                // Create unique image build tag e.g. 10.0.1.Final-2
                                def version = image.version
                                dockerTagAndPush imageBuild, "${fqImageName}:${version}"

                                // Create Stream tag e.g. 10.0
                                dockerTagAndPush imageBuild, "${fqImageName}:${version.tokenize('.')[0,1].join('.')}"

                                if (params.pushLatestTag) {
                                    dockerTagAndPush imageBuild, fqImageName
                                }
                            }
                        }
                    }
                }
            }
        }

        stage('Tag') {
            when {
                expression { !params.dryRun }
            }
            steps {
                script {
                    IMAGES.each { name, image ->
                        sh "git add ${image.descriptor}"
                        sh "git -c user.name='Infinispan' -c user.email='infinispan@infinispan.org' commit -m 'Releasing Version ${image.version} of ${name} image'"
                        sh "git tag ${name}-${image.version}"
                        sh "git push origin ${name}-${image.version}"
                    }
                    sh "git push origin ${params.branch}"
                }
            }
        }
    }

    post {
        failure {
            echo "post build status: failure"
            emailext to: '${DEFAULT_RECIPIENTS}', subject: '${DEFAULT_SUBJECT}', body: '${DEFAULT_CONTENT}'
        }

        success {
            echo "post build status: success"
            emailext to: '${DEFAULT_RECIPIENTS}', subject: '${DEFAULT_SUBJECT}', body: '${DEFAULT_CONTENT}'
        }

        cleanup {
            sh 'git clean -fdx || echo "git clean failed, exit code $?"'
            sh 'docker container prune -f'
            sh 'docker rmi $(docker images -f "dangling=true" -q) || true'
        }
    }
}
